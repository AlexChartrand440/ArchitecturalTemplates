import org.scaledl.architecturaltemplates.repositories.cloudscale.black.ProfilesLibrary;

modeltype PRM uses 'http://simulizar.palladiosimulator.org/RuntimeMeasurement/1.0';
modeltype PCM_ALLOC uses 'http://sdq.ipd.uka.de/PalladioComponentModel/Allocation/5.0';
modeltype PCM_RES_ENV uses 'http://sdq.ipd.uka.de/PalladioComponentModel/ResourceEnvironment/5.0';

transformation ScaleDown(in prm : PRM, inout pcmAllocation : PCM_ALLOC);
property scaleDownStepSize : Real;
property scaleDownThreshold : Real;
property minRate : Real;

main() {
log ('AT Completion "ScaleDown" started');	
	
	assert fatal(prm.rootObjects()[RuntimeMeasurement]->size() > 0)
		with log ("No Measurements found!");
			
	assert fatal(pcmAllocation.rootObjects()[Allocation]->size() > 0)
		with log ("Allocation Model is empty!");
		
	var allocation : Allocation := pcmAllocation.rootObjects()![Allocation];
	var resourceEnvironment : ResourceEnvironment := allocation.targetResourceEnvironment_Allocation;
	var resourceContainers : Set(ResourceContainer) := resourceEnvironment.resourceContainer_ResourceEnvironment;
	assert fatal(hasAppliedStereotype(resourceContainers,"VirtualizedResourceContainer"))
		with log ("There is no Virtualized Stereotype Application!");
		
	resourceContainers -> forEach(resourceContainer){
		if(hasAppliedStereotype(resourceContainer,"VirtualizedResourceContainer")){
			scaleDownThreshold := getDoubleTaggedValue(resourceContainer,"scaleDownThreshold","VirtualizedResourceContainer");
			scaleDownStepSize := getIntTaggedValue(resourceContainer,"stepSize","VirtualizedResourceContainer");
			minRate := getIntTaggedValue(resourceContainer,"minRate","VirtualizedResourceContainer");
			resourceContainer. map scaleDownVirtualizedContainer();
		};
	};
	log ('AT Completion "ScaleUp" finished');	
}
// FIXME need to check whether it's the right model element
helper Set(RuntimeMeasurement) :: checkCondition() : Boolean
{
	self->forEach(measurement) {
		log('Measured value is ' + measurement.measuringValue.toString());
		if (measurement.measuringValue < scaleDownThreshold) {
			return true;
		};
	};
		
	log('No measurements match the condition. Number of measurements is ' + self->size().toString());
	return false;
}

mapping ResourceContainer::scaleDownVirtualizedContainer():ResourceContainer 
	when{prm.rootObjects()[RuntimeMeasurement]->checkCondition()}
{
	log ('Scaling Up ResourceContainer ' + self.entityName);
	scaleDownProcessingResourcesRecursively(self);
}

/**
 * Recursively scale up ProcessingResources.
 */
helper scaleDownProcessingResourcesRecursively(resourceContainer : ResourceContainer) {

	// decrease processing rate by scaleDown
	resourceContainer.activeResourceSpecifications_ResourceContainer->forEach(processingResourceSpecification){
		var currentProcessingRate : String := processingResourceSpecification.processingRate_ProcessingResourceSpecification.specification;
    	var newProcessingRate : Real := currentProcessingRate.toReal() - scaleDownStepSize;
    	if(newProcessingRate >= minRate){
    		processingResourceSpecification.processingRate_ProcessingResourceSpecification.specification := newProcessingRate.toString();
    	};
    };
    
    // recursive call
	resourceContainer.nestedResourceContainers__ResourceContainer->forEach(nestedResourceContainer){
		scaleDownProcessingResourcesRecursively(nestedResourceContainer);
	};		
}